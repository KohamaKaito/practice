plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.12-SNAPSHOT'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id 'war'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/snapshot' }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-web-services'
	implementation 'org.jetbrains:annotations:24.0.0'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	//　Domaのインストール
	implementation 'org.seasar.doma.boot:doma-spring-boot-starter:1.6.0'
	annotationProcessor 'org.seasar.doma.boot:doma-spring-boot-starter:1.6.0'
	annotationProcessor 'org.seasar.doma:doma-processor:2.50.0'
	// validation
	implementation 'org.springframework.boot:spring-boot-starter-validation'
}

tasks.named('test') {
	useJUnitPlatform()
}

//  doma の gradle設定について、下記を参照してください。
//      https://doma.readthedocs.io/en/2.20.0/build/（日本語訳ページ）

// テンポラリディレクトリのパスを定義する
ext.domaResourcesDir = "${buildDir}/tmp/doma-resources"

// ---- debug用 -------------------------------------
//  println "processResources.destinationDir is ${processResources.destinationDir}"
//  println "ext.domaResourcesDir            is ${ext.domaResourcesDir}"
//  println "compileJava.destinationDir      is ${compileJava.destinationDir}"

// domaが注釈処理で参照するリソースをテンポラリディレクトリに抽出
task extractDomaResources(type: Copy, dependsOn: processResources)  {
	from processResources.destinationDir
	include 'doma.compile.config'
	include 'META-INF/**/*.sql'
	include 'META-INF/**/*.script'
	into domaResourcesDir
}

// テンポラリディレクトリ内のリソースをcompileJavaタスクの出力先ディレクトリにコピーする
task copyDomaResources(type: Copy, dependsOn: extractDomaResources)  {
	from domaResourcesDir
	into compileJava.destinationDir
}

compileJava {
	// 上述のタスクに依存させる
	dependsOn copyDomaResources
	// テンポラリディレクトリをcompileJavaタスクの入力ディレクトリに設定する
	inputs.dir domaResourcesDir
	options.encoding = 'UTF-8'
}

repositories {
	mavenCentral()
	mavenLocal()
	maven {url 'https://oss.sonatype.org/content/repositories/snapshots/'}
}

// ★warファイル名を指定★
bootWar {
	duplicatesStrategy 'INCLUDE'
	archiveName 'practice.war'
}
